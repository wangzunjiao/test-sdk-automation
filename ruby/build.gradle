import com.adyen.sdk.Service
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'adyen.sdk-automation-conventions'
}

project.ext {
    generator = 'ruby'
}

List<Service> services = project.ext.services

// Service renaming
Map<String, String> serviceNaming = project.ext.serviceNamingCamel
serviceNaming.putAll([
        'balancecontrol': 'balanceControlService',
])

services.<Service> each { Service svc ->
    String serviceName = serviceNaming[svc.id]

    // Generation
    tasks.named("generate${svc.name}", GenerateTask) {
        configFile.set("$projectDir/config.yaml")
        additionalProperties.putAll([
                'serviceName': serviceName,
        ])
    }

    // Deployment
    def deployServices = tasks.register("deploy${svc.name}Services", Sync) {
        group 'deploy'
        description "Deploy $svc.name into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }
        onlyIf { !svc.small && !svc.webhook }

        from layout.buildDirectory.dir("services/$svc.id/lib/openapi_client/api")
        include '*_api.rb'
        into layout.projectDirectory.dir("repo/lib/adyen/services/" + serviceName)

        from(layout.buildDirectory.dir("services/$svc.id/api")) {
            include 'api-single.rb'
            rename 'api-single.rb', serviceName + '.rb'
            into '..'
        }
    }

    def deploySmallService = tasks.register("deploy${svc.name}SmallService", Copy) {
        group 'deploy'
        description "Copy $svc.name into the repo."
        dependsOn "generate$svc.name"
        outputs.upToDateWhen { false }
        onlyIf { svc.small }

        from layout.buildDirectory.file("services/$svc.id/lib/openapi_client/api/default_api-small.rb")
        rename 'default_api-small.rb', "${serviceName}.rb"
        into layout.projectDirectory.dir("repo/lib/adyen/services")
    }

    tasks.named(svc.id) { dependsOn deployServices, deploySmallService }
}

tasks.named('deployDataProtectionSmallService', Copy) {
    filter {
        it.replace("'DataProtection'", "'DataProtectionService'")
    }
}

// Tests
tasks.named('checkout') {
    doLast {
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/checkout/payments_api.rb").exists()
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/checkout.rb").exists()
    }
}
tasks.named('binlookup') {
    doLast {
        assert file("${layout.projectDirectory}/repo/lib/adyen/services/binLookup.rb").exists()
        assert !file("${layout.projectDirectory}/repo/lib/adyen/services/binLookup").exists()
    }
}
